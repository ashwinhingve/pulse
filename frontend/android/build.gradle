// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.9.22'

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
        classpath 'com.google.gms:google-services:4.4.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

// Fix Kotlin stdlib duplicate classes
subprojects {
    configurations.all {
        resolutionStrategy {
            force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        }
    }
}

// Capacitor 8.x requires Java 21. Explicitly request the JDK 21 toolchain
// for all Java compilation tasks across every subproject.
// Combined with the Foojay resolver in settings.gradle, Gradle will
// auto-download JDK 21 if it is not already installed.
subprojects {
    afterEvaluate {
        tasks.withType(JavaCompile).configureEach {
            javaCompiler = javaToolchains.compilerFor {
                languageVersion = JavaLanguageVersion.of(21)
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
